<!-- vim: syntax=markdown -->

# Day 2

## Setup

```elixir
input = File.read!("/Users/andreicek/Workspace/aoc2021/day-02.txt")
```

## Part 1

```elixir
input
|> String.split("\n", trim: true)
|> Enum.reduce({_pos = 0, _depth = 0}, fn
  "forward " <> n, {pos, depth} -> {pos + String.to_integer(n), depth}
  "down " <> n, {pos, depth} -> {pos, depth + String.to_integer(n)}
  "up " <> n, {pos, depth} -> {pos, depth - String.to_integer(n)}
end)
|> then(fn {pos, depth} -> pos * depth end)
```

## Part 2

```elixir
input
|> String.split("\n", trim: true)
|> Enum.reduce({_pos = 0, _depth = 0, _aim = 0}, fn
  "forward " <> n, {pos, depth, aim} ->
    number = String.to_integer(n)
    {pos + number, depth + number * aim, aim}

  "down " <> n, {pos, depth, aim} ->
    {pos, depth, aim + String.to_integer(n)}

  "up " <> n, {pos, depth, aim} ->
    {pos, depth, aim - String.to_integer(n)}
end)
|> then(fn {pos, depth, _aim} -> pos * depth end)
```
